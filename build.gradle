plugins {
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.google.cloud.tools.jib' version '3.2.1'
    id "com.github.spotbugs" version "5.0.6"

    id 'java'
    id 'jacoco'
    id 'checkstyle'
}

group = 'com.gtlab.godjigame.wax'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    maven {
        url = project.findProperty("package_registry") ?: System.getenv("PACKAGE_REGISTRY")
        credentials {
            username = project.findProperty("package_username") ?: System.getenv("PACKAGE_USERNAME")
            password = project.findProperty("package_password") ?: System.getenv("PACKAGE_PASSWORD")
        }
    }
    mavenCentral()
}

configurations.all {
    exclude module: 'slf4j-jdk14'
}

dependencies {
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:3.1.3'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'

    implementation 'one.block:eosiojava:1.0.0'
    implementation 'one.block:eosiojavasoftkeysignatureprovider:1.0.0'
    implementation 'one.block:eosio-java-abieos-serialization-provider:1.0.0'

    implementation 'org.json:json:20220320'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

checkstyle {
    toolVersion '10.1'
}

spotbugs {
    version '4.6.0'
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
        }
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        html.required = true
        csv.required = true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'com/gtlab/godjigame/wax/eosioproxyservice/rpc/**',
                    'com/gtlab/godjigame/wax/eosioproxyservice/exceptions/**',
                    'com/gtlab/godjigame/wax/eosioproxyservice/dto/**'
            ])
        }))
    }
}
