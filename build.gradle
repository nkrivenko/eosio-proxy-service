plugins {
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.google.cloud.tools.jib' version '3.2.1'
    id "com.github.spotbugs" version "5.0.6"

    id 'java'
    id 'jacoco'
    id 'checkstyle'
}

group = 'com.gtlab.godjigame.wax'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

configurations.all {
    exclude module: 'slf4j-jdk14'
}

dependencies {
    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.11.0'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    implementation('one.block:eosiojava:1.0.0')
    implementation('one.block:eosiojavasoftkeysignatureprovider:1.0.0')
    implementation('one.block:eosio-java-rpc-provider:1.0.0')

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

checkstyle {
    toolVersion '10.1'
}

spotbugs {
    version '4.6.0'
}

spotbugsMain {
    reports {
        html {
            required = true
            outputLocation = file("$buildDir/reports/spotbugs/main/spotbugs.html")
        }
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = false
        html.required = false
        csv.required = true
    }
}
